<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:iso="bitrise.prostokvashino.base.view.iso.*" width="900" height="600" creationComplete="creationCompleteHandler(event)" >
	
	<fx:Script>
		<![CDATA[
			import bitrise.core.utils.GeomUtils;
			import bitrise.prostokvashino.base.points.Building;
			import bitrise.prostokvashino.base.points.BuildingSprite;
			import bitrise.prostokvashino.base.points.Graph;
			import bitrise.prostokvashino.base.points.Map;
			import bitrise.prostokvashino.base.points.PointBase;
			import bitrise.prostokvashino.base.points.RoadBase;
			import bitrise.prostokvashino.editor.FileBrowser;
			import bitrise.prostokvashino.editor.SelectImageWindow;
			import bitrise.prostokvashino.editor.points.EditorBuilding;
			import bitrise.prostokvashino.editor.points.EditorMap;
			import bitrise.prostokvashino.editor.points.EditorPoint;
			import bitrise.prostokvashino.editor.points.EditorRoad;
			import bitrise.prostokvashino.editor.points.EditorSprite;
			
			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import spark.components.Image;
			
			
			public static const images:ArrayCollection = FileBrowser.brows("libs/scene/");
			
			[Bindable]
			public var map:EditorMap = new EditorMap();
			
			[Bindable]
			private var road:EditorRoad;
			
			[Bindable]
			private var point:EditorPoint;
			
			[Bindable]
			private var graph:Graph;
			
			[Bindable]
			private var buildable:Boolean;
			private var building:EditorBuilding;
			private var buildingRemove:Boolean = false;
			
			protected function selectImageClickHandler(event:MouseEvent):void
			{
				const window:SelectImageWindow = new SelectImageWindow();
				PopUpManager.addPopUp(window, FlexGlobals.topLevelApplication as DisplayObject, true);
				PopUpManager.centerPopUp(window);
			}
			
			protected function createRoadHandler(event:MouseEvent):void {
				removeToools();
				road = map.addRoad(new EditorRoad()) as EditorRoad;
				road.select = true;
				point = road.addPoint(new EditorPoint()) as EditorPoint;
				point.select = true;
				point.temp = true;
				highlight.mouseChildren = highlight.mouseEnabled = false;
			}
			
			protected function sceneClickHandler(event:MouseEvent):void {
				const localX:Number = scene.mouseX;
				const localY:Number = scene.mouseY;
				const buildingContainer:BuildingSprite = event.target as BuildingSprite;
				if (road && point) {
					if (buildingContainer && GeomUtils.lineLength( buildingContainer.x + buildingContainer.width / 2, buildingContainer.y + buildingContainer.height / 2, localX, localY ) <= 25 ) {
						if ((buildingContainer.building as EditorBuilding).point || point.building)
							return;
						point.building = buildingContainer.building as EditorBuilding;
						(buildingContainer.building as EditorBuilding).point = point;
						point.x = buildingContainer.x + buildingContainer.width / 2;
						point.y = buildingContainer.y + buildingContainer.height / 2;
						point.temp = false;
						point.select = false;
						poinCompleteClickHandler(null);
					} else {
						const ps:Vector.<EditorPoint> = map.getPoints(localX, localY, 10);
						if (ps.length) {
							var min:EditorPoint = ps[0];
							for each(var p:EditorPoint in ps) {
								if (p.lastLength <= min.lastLength && p != point)
									min = p;
							}
							point.x = min.x;
							point.y = min.y;
						}
						
						if (point.temp) {
						
							point.select = false;
							point = road.addPoint(new EditorPoint()) as EditorPoint;
							highlight.mouseChildren = highlight.mouseEnabled = false;
							point.select = true;
							point.temp = true;
							
						}
					}
				} else if (buildable) {
					if (buildingRemove) {
						if (buildingContainer)
							building = buildingContainer.building as EditorBuilding;
						removeBuildClickHandler(null);
						buildingRemove = false;
					} else if (building) {
						building.sprite.addEventListener(MouseEvent.ROLL_OVER, handler_buildingOver);
						building.sprite.addEventListener(MouseEvent.ROLL_OUT, handler_buildingOut);
						building = null;
					}	
				} else {
					const sprite:EditorSprite = event.target as EditorSprite;
					if (sprite) {
						if (fromTo) {
							const vector:Vector.<EditorPoint> = sprite.road.getPoints(localX, localY, 10);
							if (vector.length) {
								const node:PointBase = graph.getNode(vector[0].x, vector[0].y);
								if (node) {
									fromTo.push(node);
									redrawFromTo();
								}
							}
						} else {
							road = sprite.road;
							highlight.mouseChildren = highlight.mouseEnabled = true;
							road.select = true;
							road.redraw();
						}
					}
				}
			}
			
			protected function sceneMouseMoveHandler(event:MouseEvent):void {
				const localX:Number = scene.mouseX;
				const localY:Number = scene.mouseY;
				if (point && road) {
					point.move(localX, localY);
					road.redraw();
				} else if (building) {
					building.move(localX - building.sprite.width / 2, localY - building.sprite.height / 2);
				}
			}
			
			protected function poinCompleteClickHandler(event:MouseEvent):void
			{
				if (road && point) {
					point.select = false;
					if (point.temp) {
						road.removePoint(point);
					}
					point = null;
					road.redraw();
				}
				highlight.mouseChildren = highlight.mouseEnabled = true;
				roadCompleteClickHandler(null);
			}
			
			protected function roadCompleteClickHandler(event:MouseEvent):void
			{
				if (road) {
					road.select = false;
					if (point) {
						point.select = false;
						if (point.temp) {
							road.removePoint(point);
						}
						point = null;
					}
					if (road.vector.length > 1) {
						road.redraw();
					} else {
						map.removeRoad(road);
					}
					road = null;
				}
				highlight.mouseChildren = highlight.mouseEnabled = true;
			}
			
			protected function roadRemoveClickHandler(event:MouseEvent):void {
				if (road) {
					for each(var p:EditorPoint in road.vector) {
						if (p.building) {
							p.building.point = null;
							p.building = null;
						}
					}
					map.removeRoad(road);
					road = null;
					point = null;
				}
				highlight.mouseChildren = highlight.mouseEnabled = true;
			}
			
			protected function checkMapClickHandler(event:MouseEvent):void {
				removeToools();
				graph = new Graph(map);
				fromTo = null;
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				map.sprite = highlight;
			}
			
			private var fromTo:Vector.<PointBase>;
			
			protected function seletFromToClickHandler(event:MouseEvent):void
			{
				fromTo = new Vector.<PointBase>();
			}
			
			protected function redrawFromTo():void {
				if (fromTo) {
					graphed.graphics.clear();
					for each(var point:PointBase in fromTo) {
						graphed.graphics.beginFill(0xFF00FF, 1);
						graphed.graphics.drawCircle(point.x, point.y, 5);
						graphed.graphics.endFill();
					}
					if (fromTo.length > 1) {
						const vector:Vector.<PointBase> = graph.pathNodeToNode(fromTo.pop(), fromTo.pop());
						if (vector) {
							
							graphed.graphics.lineStyle(3, 0xFF00FF, 1);
							graphed.graphics.moveTo(vector[0].x, vector[0].y);
							vector.shift();
							for each(point in vector) {
								graphed.graphics.lineTo(point.x, point.y);
							}
						}
						fromTo.length = 0;
					}
				} else {
					graphed.graphics.clear();
				}
			}
			
			protected function graphCompleteClickHandler(event:MouseEvent):void {
				graph = null;
				fromTo = null;
				graphed.graphics.clear();
			}
			
			protected function linkedClickHandler(event:MouseEvent):void {
				removeToools();
			}
			
			private function removeToools():void {
				roadCompleteClickHandler(null);
				graphCompleteClickHandler(null);
				buildCompleteClickHandler(null);
			}
			
			protected function buildClickHandler(event:MouseEvent):void {
				removeToools();
				buildable = true;
			}
			
			private function build(source:Class):void {
				removeBuildClickHandler(null);
				buildingRemove = false;
				building = map.addBuilding(new EditorBuilding(source)) as EditorBuilding;
			}
			
			protected function removeBuildClickHandler(event:MouseEvent):void {
				if (building) {
					if (building.point) {
						building.point.building = null;
						building.point = null;
					}
					map.removeBuilding(building);
					building.sprite.addEventListener(MouseEvent.ROLL_OVER, handler_buildingOver);
					building.sprite.addEventListener(MouseEvent.ROLL_OUT, handler_buildingOut);
					building = null;
				} else {
					buildingRemove = true;
				}
			}
			
			protected function buildCompleteClickHandler(event:MouseEvent):void {
				removeBuildClickHandler(null);
				buildingRemove = false;
				buildable = false;
			}
			
			private function handler_buildingOver(event:Event):void {
				const container:BuildingSprite = event.target as BuildingSprite;
				if (road && point && container) {
					const shape:Shape = new Shape();
					shape.name = "shape";
					shape.graphics.clear();
					shape.graphics.lineStyle(3, 0xFF00FF, 1);
					shape.graphics.drawCircle(container.width / 2, container.height / 2, 20);
					container.addChild(shape);
				}
			}
			
			private function handler_buildingOut(event:Event):void {
				const container:BuildingSprite = event.target as BuildingSprite;
				if (container && container.getChildByName("shape"))
					container.removeChild(container.getChildByName("shape"));
			}
			
			protected function saveClickHandler(event:MouseEvent):void {
				removeToools();
				const file:File = new File();
				file.save(map.serealize(), "default_map.js");
			}
			
			private var load:File;
			protected function loadClickHandler(event:MouseEvent):void {
				removeToools();
				load = new File();
				load.browse([new FileFilter("JSON", "*.js")]);
				load.addEventListener(Event.SELECT, onFileSelected);
			}
			
			protected function onFileSelected(e:Event):void {
				load.addEventListener(Event.COMPLETE, onFileLoaded);
				load.load();
			}
			
			protected function onFileLoaded(e:Event):void {
				map.deserialize(e.target.data);
			}
			
		]]>
	</fx:Script>
	
	<s:Group width="100%" height="100%">
		<s:layout>
			<s:HorizontalLayout verticalAlign="middle" horizontalAlign="center" />
		</s:layout>
		<s:Group click="sceneClickHandler(event)" 
				 mouseMove="sceneMouseMoveHandler(event)" >
			<iso:Scene id="scene" width="800" height="450" map="{map}" />
			<mx:UIComponent id="highlight" width="800" height="450" />
			<mx:UIComponent id="graphed" width="800" height="450" mouseEnabled="false" mouseChildren="false" />
		</s:Group>
	</s:Group>
	
	<s:HGroup top="5" left="5">
		<s:Button label="Фон" click="selectImageClickHandler(event)" />
		<s:Button label="Маршрут" click="createRoadHandler(event)" />
		<s:Button label="Построить" click="buildClickHandler(event)" />
		<s:Button label="Проверить" click="checkMapClickHandler(event)" />
		<s:Button label="Сохранить" click="saveClickHandler(event)" />
		<s:Button label="Загрузить" click="loadClickHandler(event)" />
	</s:HGroup>
	
	<s:HGroup bottom="5" left="5">
		<s:HGroup visible="{road != null}" includeInLayout="{road != null}" verticalAlign="middle" >
			<s:Label text="Маршрут: " />
			<s:Button label="Удалить" click="roadRemoveClickHandler(event)" />
			<s:Button label="Завершить" click="roadCompleteClickHandler(event)" />
		</s:HGroup>
		<s:HGroup visible="{point != null}" includeInLayout="{point != null}" verticalAlign="middle" >
			<s:Label text="Точка: " />
			<s:Button label="Завершить" click="poinCompleteClickHandler(event)" />
		</s:HGroup>
		<s:HGroup visible="{graph != null}" includeInLayout="{graph != null}" verticalAlign="middle" >
			<s:Label text="Граф: " />
			<s:Button label="Выбрать" click="seletFromToClickHandler(event)" />
			<s:Button label="Завершить" click="graphCompleteClickHandler(event)" />
		</s:HGroup>
		<s:HGroup visible="{buildable}" includeInLayout="{buildable}" verticalAlign="middle" >
			<s:Label text="Постройка: " />
			<s:Button label="Завод" click="build(EmbedBuilding.zavod)" />
			<s:Button label="Ферма" click="build(EmbedBuilding.farm)" />
			<s:Button label="Магазин" click="build(EmbedBuilding.shop)" />
			<s:Button label="Удалить" click="removeBuildClickHandler(event)" />
			<s:Button label="Завершить" click="buildCompleteClickHandler(event)" />
		</s:HGroup>
	</s:HGroup>
	
	
</s:WindowedApplication>
